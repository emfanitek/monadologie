/*
 * Monadic excercies in Groovy
 * ---------------------------
 * build script
 *
 * © 2011, Dinko Srkoč
 */

apply plugin:'groovy'
apply plugin: 'maven'
apply plugin: 'idea'

group = 'hr.helix'
version = '0.2.1'

repositories {
    mavenCentral()
}

configurations {
    deployerJars
}

dependencies {
    compile group:'org.codehaus.groovy', name:'groovy-all', version:'1.8.8', ext:'jar'
    testCompile 'org.spockframework:spock-core:0.7-groovy-1.8'
}

sourceSets {
    main {
        groovy {
            srcDir 'src'
        }
    }
    test {
        groovy {
            srcDir 'test'
        }
    }
}

task srcJar(type: Jar) {
    classifier = 'src'
    from sourceSets.main.allGroovy
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

artifacts { project ->
    archives srcJar
    archives groovydocJar
}

    uploadArchives {
        repositories.mavenDeployer {
            uniqueVersion = true
            name = 'httpDeployer'
            configuration = configurations.deployerJars
            def repoURL = "http://artifactory:8081/artifactory/libs-snapshots-local"

            repository(url: repoURL) {
                authentication(userName: "ivyrep", password: "1vyRep0s1t0ry")
            }
        }
    }
